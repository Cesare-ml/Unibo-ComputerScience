// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input
 * holds the value of this location. If the current instruction needs
 * to write a value to M, the value is placed in outM, the address
 * of the target location is placed in the addressM output, and the
 * writeM control bit is asserted. (When writeM==0, any value may
 * appear in outM). The outM and writeM outputs are combinational:
 * they are affected instantaneously by the execution of the current
 * instruction. The addressM and pc outputs are clocked: although they
 * are affected by the execution of the current instruction, they commit
 * to their new values only in the next time step. If reset==1 then the
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather
 * than to the address resulting from executing the current instruction.
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M?
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    //Decodifico instruction
    And(a=instruction[15], b=true, out=CcInstruction);
    Not(in=CcInstruction, out=CaInstruction);

    And(a=instruction[0],  b=CcInstruction, out=Cj3);
    And(a=instruction[1],  b=CcInstruction, out=Cj2);
    And(a=instruction[2],  b=CcInstruction, out=Cj1);
    And(a=instruction[3],  b=CcInstruction, out=Cd3, out=writeM);
    And(a=instruction[4],  b=CcInstruction, out=Cd2);
    And(a=instruction[5],  b=CcInstruction, out=Cd1);
    And(a=instruction[6],  b=CcInstruction, out=Cc6);
    And(a=instruction[7],  b=CcInstruction, out=Cc5);
    And(a=instruction[8],  b=CcInstruction, out=Cc4);
    And(a=instruction[9],  b=CcInstruction, out=Cc3);
    And(a=instruction[10], b=CcInstruction, out=Cc2);
    And(a=instruction[11], b=CcInstruction, out=Cc1);
    And(a=instruction[12], b=CcInstruction, out=Ca);

    //Collego i componenti
    Mux16(a=aluOut, b=instruction, sel=CaInstruction, out=aRegisterInput);
    Or(a=Cd1, b=CaInstruction, out=loadToA);
    ARegister(in=aRegisterInput, load=loadToA, out=aRegisterOut, out[0..14]=addressM);
    DRegister(in=aluOut, load=Cd2, out=dRegisterOut);

    Mux16(a=aRegisterOut, b=inM, sel=Ca, out=aluInputAM);
    ALU(x=dRegisterOut, y=aluInputAM, zx=Cc1, nx=Cc2, zy=Cc3, ny=Cc4, f=Cc5, no=Cc6, out=aluOut, out=outM, zr=aluZr, ng=aluNg);

    PC(in=aRegisterOut, load=needToJump, inc=true, reset=reset, out[0..14]=pc);
    
    //Calcolo se c'Ã© bisogno di un jump
    Not(in=aluZr, out=aluNotZr);
    Not(in=aluNg, out=aluGE);
    And(a=aluNotZr, b=aluGE, out=aluGT);

    And(a=Cj3, b=aluGT, out=jumpCase1);
    And(a=Cj2, b=aluZr, out=jumpCase2);
    And(a=Cj1, b=aluNg, out=jumpCase3);

    Or(a=jumpCase1, b=jumpCase2, out=needToJumpTmp1);
    Or(a=needToJumpTmp1, b=jumpCase3, out=needToJump);
}
